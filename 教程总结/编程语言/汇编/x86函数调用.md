https://zhuanlan.zhihu.com/p/24129384

函数调用都发生了什么：
保存现场（一会好回来接着做）
传递参数（可选，套公式的时候需要些什么数据）
返回（把计算结果带回来，接着刚才的事）

函数调用示例
```
global main

eax_plus_1s:
    add eax, 1
    ret

ebx_plus_1s:
    add ebx, 1
    ret

main:
    mov eax, 0
    mov ebx, 0
    call eax_plus_1s  //调用函数命令
    call eax_plus_1s
    call ebx_plus_1s
    add eax, ebx
    ret
```

跳转指令jmp，函数调用指令call，这两个指令都能让CPU的eip寄存器发生突然变化，然后程序就一下子跳到别的地方去了。但是这两个有区别：

很简单，jmp跳过去了就不知道怎么回来了，而通过call这种方式跳过去后，是可以通过ret指令直接回来的

那这是怎么做到的呢？

其实，在call指令执行的时候，CPU进行跳转之前还要做一个事情，就是把eip保存起来，然后往目标处跳。当遇到ret指令的时候，
就把上一次call保存起来的eip恢复回来，我们知道eip直接决定了CPU会执行哪里的代码，当eip恢复的时候，就意味着程序又会到之前的位置了。

一个程序免不了有很多次call，那这些eip的值都是保存到哪里的呢？

有一个地方叫做“栈(stack)”，是程序启动之前，由操作系统指定的一片内存区域，每一次函数调用后的返回地址都存放在栈里面


函数调用总结起来就是：
本质上也是跳转，但是跳到目标位置之前，需要保存“现在在哪里”的这个信息，也就是eip
整个过程由一条指令call完成
后面可以用ret指令跳转回来
call指令保存eip的地方叫做栈，在内存里，ret指令执行的时候是直接取出栈中保存的eip值，并恢复回去达到返回的效果


CPU的栈与数据结构的栈
CPU的栈是CPU内硬件实现的栈
数据结构中的栈是通过编程语言来形成程序执行逻辑上的栈
相同点：栈的逻辑是相同的，符合先进后出

数据结构使用基于数组实现的栈
需要一个栈顶指针（或者是一个索引、下标），每次放东西入栈，就将指针后移，每一次从栈中取出东西来，就将指针前移

CPU在发生函数调用的过程中是如何使用栈的
假设现在程序处在一个叫做level1的位置，并调用了函数A，在调用的跳转发生之前，会将当前的eip保存起来，这时候，栈里面就是这样的：
```
----------    <=   top
  level1
----------
```
现在，程序处在level2的位置，又调用了函数B，同样，也会保存这次的eip进去：
```
----------    <=   top
  level2
----------
  level1
----------
```
程序这次处在level3，调用了C函数，这时候，整个栈就是这样的：
```
----------    <=   top
  level3
----------
  level2
----------
  level1
----------
```
当程序执行到了ret，会发生什么事，是不是就回到level3了？在level3中再次执行ret，是不是就回到level2了？
以此类推，最终，程序就能做到一层层的函数调用和返回了

栈顶top也是由一个寄存器来记录的，这个寄存器叫做esp(stack pointer)
esp记录着当前栈顶所在的位置，每次call和ret执行都会伴随着入栈和出栈，也就是esp会发生变化

在x86的环境下，栈是朝着低地址的方向伸长的。什么意思呢？每一次有东西入栈，那么栈顶指针就会递减一个单位，每一次出栈，
栈顶指针就会相应地增加一个单位（和数据结构中一般的做法是相反的）
eip在入栈的时候，大致就相当于执行了这样一些指令：
```
sub esp, 4
mov dword ptr[esp], eip
```
翻译为C语言就是（假如esp是一个void*类型的指针）：
```
esp = (void*)( ((unsigned int)esp) - 4 )
*( (unsigned int*) esp ) = (unsigned int) eip
```
也就是esp先移动，然后再把eip的值写入到esp指向的内存中。那么，ret执行的时候该干什么，也就非常的清楚了吧。无非就是上述过程的逆过程。
同时，eip寄存器的长度为32位，即4字节，所以每一次入栈出栈的单位大小都是4字节

函数调用分析
```
global main

eax_plus_1s:
    add eax, 1
    ret

main:
    mov eax, 0
    call eax_plus_1s
    ret
```
拿到反汇编 disas main
```
Dump of assembler code for function main:
   0x08049164 <+0>:     mov    $0x0,%eax
   0x08049169 <+5>:     call   0x8049160 <eax_plus_1s>
   0x0804916e <+10>:    ret    
   0x0804916f <+11>:    nop
End of assembler dump.
```
在这个call指令处打下一个断点，然后运行程序。
```
(gdb) b *0x08049169
Breakpoint 1 at 0x8049169
(gdb) run
Starting program: .../汇编/代码示例/first 

Breakpoint 1, 0x08049169 in main ()
```
再来看反汇编，这次有一个小箭头指向当前的断点了：
```
Dump of assembler code for function main:
   0x08049164 <+0>:     mov    $0x0,%eax
=> 0x08049169 <+5>:     call   0x8049160 <eax_plus_1s>
   0x0804916e <+10>:    ret    
   0x0804916f <+11>:    nop
End of assembler dump.
```
看看现在eip的值是多少：
```
(gdb) info register eip
eip            0x8049169           0x8049169 <main+5>
```
正好指向这个函数调用指令。这里的call指令还没执行，现在的CPU处在上一条指令刚执行完毕的状态
查看esp的值
```
(gdb) info register esp
esp            0xffffbc5c          0xffffbc5c
```
esp所指向的栈顶的东西
```
(gdb) p/x *(unsigned int*)$esp
$1 = 0xf7de2b41
```
再走一步，进入函数，并查看反汇编
```
(gdb) stepi
0x08049160 in eax_plus_1s ()  //根据地址提示，程序现在已经执行到函数里面去了
(gdb) disas   //注意此时没有main，不查看main函数
Dump of assembler code for function eax_plus_1s:
=> 0x08049160 <+0>:     add    $0x1,%eax
   0x08049163 <+3>:     ret    
End of assembler dump.
```
现在正等着执行那条加法指令，函数调用已经发生了
再次查看esp
```
(gdb) info register esp
esp            0xffffbc58          0xffffbc58
```
上一次是0xffffbc5c，本次是0xffffbc58，少了4
实际上这就是eip被保存到栈里去了，CPU的栈的伸长方向是朝着低地址一侧的，所以每次入栈，esp都会减少一个单位，也就是4
esp所指向的栈顶的东西
```
(gdb) p/x *(unsigned int*)$esp
$2 = 0x804916e
```
0x804916e代表什么呢
在里面找找0x80483fe呢？刚好在<+10>所在的那一行。这不就是函数调用指令处的后一条指令吗？   此时0x804916e高位多个0变为0x0804916e
```
Dump of assembler code for function main:
   0x08049164 <+0>:     mov    $0x0,%eax
=> 0x08049169 <+5>:     call   0x8049160 <eax_plus_1s>
   0x0804916e <+10>:    ret    
   0x0804916f <+11>:    nop
End of assembler dump.
```
一会函数返回的时候，就会到<+10>这个地方来。也就是在执行了eax_plus_1s函数里的ret之后
函数在stack中分布，从高地址到低地址
main            0x0804916e <+10>:    ret
                0x08049169 <+5>:     call   0x8049160 <eax_plus_1s>
                0x08049164 <+0>:     mov    $0x0,%eax
eax_plus_1s     0x08049163 <+3>:     ret
                0x08049160 <+0>:     add    $0x1,%eax

eip中的值  0x8049164   main mov               esp  0xffffbc5c 
          0x8049169   main call              esp  0xffffbc5c   
          0x8049160   eax_plus_1s add        esp  0xffffbc58   p/x *(unsigned int*)$esp(结果$2 = 0x804916e)
          0x08049163  eax_plus_1s ret        esp  0xffffbc58
          0x804916e   main ret               esp  0xffffbc5c   p/x *(unsigned int*)$esp(结果$1 = 0xf7de2b41)
          0xf7de2b41  /lib/i386-linux-gnu/libc.so.6  __libc_start_main   esp 0xffffbc60
          ....



https://zhuanlan.zhihu.com/p/24265088
递归调用
斐波那契函数：
```
int fibo(int n) {
    if(n == 1 || n == 2) {
        return 1;
    }
    return fibo(n - 1) + fibo(n - 2);
}
```
进行一波改造，改造成接近汇编的形式：  改成每行一条指令，每行只做一步运算
```
int fibo(int n){
    if(n==1){
      return 1;
    }
    if(n==2){
      return 2;
    }
    int x = n-1;
    int y = n-2;
    int a = fibo(x);
    int b = fibo(y);
    int c = a+b;
    return c;
}
```
再改造一下，把变量名全都换成寄存器名，就能够看得更清楚了（先约定eax寄存器作为函数的第一个参数，通过eax也用来传递返回值）：
n用eax替换
因为eax会被用作参数和返回值，所以进入函数后就需要将eax保存到别的寄存器，一会需要的时候才能够更方便地使用。
```
int fibo(int eax){
    if(eax==1){
      return eax;
    }
    if(eax==2){
       eax = 1;
      return eax;
    }
    int ebx, ecx;
    
    int edx = eax; //保存eax
    
    eax = edx-1; //eax作为参数
    eax = fibo(eax); //eax作为返回值
    ebx = eax; //保存eax
    
    eax = edx-2; //使用一开始的eax
    eax = fibo(eax); //eax又一次作为参数
    ecx = eax;   //保存eax
    
    eax = ebx+ecx; //eax 作为返回值
    return c;
}
```
翻译为汇编
```
fibo:
    cmp eax, 1
    je _get_out
    cmp eax, 2
    je _get_out
    
    mov edx, eax
    sub eax, 1
    call fibo
    mov ebx, eax
    
    mov eax, edx
    sub eax, 2
    call fibo
    mov ecx, eax
    
    mov eax, ebx
    add eax, ecx
    ret
    
_get_out:
    mov eax, 1
    ret
```
初始调用为4
```
main:
    mov eax, 7
    call fibo
    ret
```
结果为6      但是fib(7)应该为13    1 1 2 3 5 8 13
问题出在哪里呢？

问题就出在从C语言翻译到汇编的过程中。

警惕作用域
在C函数中，虽然我们把各个变量名换成寄存器名，把复杂的语句拆分成简单语句，最后就能够和汇编语句等同起来，但是，在将C代码翻译到汇编的过程中，
出现了不等价的变换。其中，变量的作用域便是引起不等价的原因之一
上述汇编等价的c代码是   变量ebx，ecx，edx是全局变量
```
int ebx, ecx, edx;

void fibo() {

    if(eax == 1) {
        eax = 1;
        return;
    }
    if(eax == 2) {
         eax = 1;
         return;
    }
    
    edx = eax;
    
    eax = edx - 1;
    eax = fibo(eax);
    ebx = eax;
    
    eax = edx - 2;
    eax = fibo(eax);
    ecx = eax;
    
    eax = ebx + ecx;
}
```
原因很简单，CPU中的寄存器是全局可见的。所以使用寄存器，实际上就是在使用一个像全局变量一样的东西。

那么，到这里，通过这个例子，你应该能够发现问题了，现有的做法，无法实现递归或者嵌套的结构。

到底需要什么
实际上，要实现递归，那么就需要函数的状态是局部可见的，只能在当前这一层函数内访问。递归中会出现层层调用自己的情况，
每一层之间的状态都应当保证局部性，不能相互影响。

在C语言的环境下，函数内的局部变量，抽象来看，实际上就是函数执行时的局部状态。在汇编环境下，寄存器是全局可见的，不能用于充当局部变量。

那怎么办呢？

栈
前面说到，栈是用来保存函数调用后的返回地址。其实在这里，函数的返回地址，其实就是当前这一层函数的一个状态，
这个状态对应的是这一层函数当前执行到哪儿了。

借鉴call指令保存返回地址的思路，如果，在每一层函数中都将当前比较关键的寄存器保存到栈中，然后才去调用下一层函数，并且，
下层的函数返回的时候，再将寄存器从栈中恢复出来，这样也就能够保证下层的函数不会破坏掉上层函数的状了。

也就是，当下要解决这样一个问题：被调用函数在使用一些寄存器的时候，不能影响到调用者所使用的寄存器值，否则函数之间就很难配合好了，也很容易乱套。

入栈与出栈
实际上，CPU的设计者们已经考虑过这个问题了，所以还专门提供了对应的指令来干这个事。入栈与出栈分别是两个指令：
push eax            ; 将eax的值保存到栈中去
pop ebx             ; 将栈顶的值取出并存放到ebx中
有了这两个玩意儿，递归调用这个问题就可以解决了。注意了，这里发生了入栈和出栈的情况，那么，进行栈操作的时候对应的栈顶指针也会发生相应的移动，
这里也一样
//入栈出栈的操作
栈（stack）的特性是后入先出，数据在存储时是从内存的下层（大的地址编号）逐渐往上层（小的地址编号）累积，读出时则是按照从上往下进行读取的
Push操作从高地址到低地址
Pop操作从低地址到高地址

解决函数内保存局部状态的问题，其中的套路之一便是，让函数在使用某个寄存器之前，先把旧的值保存起来，等用完了之后再恢复回去，
那么这个函数执行完毕后，所有的寄存器都是干干净净的，不会被函数玷污

代码改造
代码中使用到了除eax之外的寄存器有ebx、ecx、edx三个。为了保证这三个寄存器不会在不同的递归层级串场，我们需要在函数内使用它们之前将其保存起来，
等到不用了之后再还原回去（注意入栈和出栈的顺序是需要反过来的）
```
fibo:
global main

fibo:
    cmp eax, 1
    je _get_out
    cmp eax, 2
    je _get_out
    
    push ebx
    push ecx
    push edx
    
    mov edx, eax
    sub eax, 1
    call fibo
    mov ebx, eax
    
    mov eax, edx
    sub eax, 2
    call fibo
    mov ecx, eax
    
    mov eax, ebx
    add eax, ecx
    
    pop edx
    pop ecx
    pop ebx
    
    ret
    
_get_out:
    mov eax, 1
    ret

main:
    mov eax, 7
    call fibo
    ret
```
结果 13




C语言中的函数
在C语言中，x86的32位环境的一般情况下，函数的参数并不是通过寄存器来传递的，返回值也得视情况而定。这取决于编译器怎么做。

实际上，一些基本数据类型，以及指针类型的返回值，一般是通过寄存器eax来传递的，也就是和前面写的汇编一个套路。而参数就不是了，
C中的参数一般是通过堆栈来传递的，而非寄存器（当然也可以用寄存器，不过需要加一些特殊的说明）。
这里准备了一个例子，供大家体会一下C语言中通过堆栈传递参数的感觉：
(在32位环境下编译)
```
#include <stdio.h>

int sum(int n, int a, ...) {
    int s = 0;
    int *p = &a;
    for(int i = 0; i < n; i ++) {
        s += p[i];
    }
    return s;
}

int main() {

    printf("%d\n", sum(5, 1, 2, 3, 4, 5));
    return 0;
}
```
编译运行：
```
$ gcc -std=c99 -m32 demo.c -o demo
$ ./demo
15
```
函数的参数是逐个放到堆栈中的，通过第一个参数的地址，可以挨着往后找到后面所有的参数。你还可以尝试把参数附近的内存都瞧一遍，
还能找到混杂在堆栈中的返回地址。

若读者想要对C的函数机制一探究竟，可以尝试编写一些简单的程序，进行反汇编，研究整个程序在汇编这个层面，到底在做些什么。